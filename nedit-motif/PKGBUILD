# Contributor: Ecmel Ercan <ecmel dot ercan at gmail dot com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: Ronny Lorenz <raumzeit@gmx.net>

pkgname=nedit-motif
pkgver=5.7
pkgrel=1
pkgdesc="A Unix text editor for programmers and general users"
dirname=nedit
arch=('i686' 'x86_64')
url="http://www.nedit.org/"
license=('GPL' 'custom')
depends=('openmotif' 'libxpm')
provides=('nedit')
conflicts=('nedit')
options=('!makeflags')
source=("http://sourceforge.net/projects/nedit/files/nedit-source/${dirname}-${pkgver}-src.tar.gz"
        "nedit-256x256.png"
        "nedit-128x128.png"
        "nedit-64x64.png"
        "nedit-48x48.png"
        "nedit-32x32.png"
        "nedit-24x24.png"
        "nedit-16x16.png"
        "nedit.desktop")

sha1sums=('7cd264860ff75b4c64417aa09ca4ce03e4be2b4e'
          '3340e5bc8e59d71b3a28b9d9ccd8c703e866f46e'
          'e2b98fdd6c46def4b467a020b3eddf8ed3cb6528'
          '17054b4aa743d04f85be3adb57e58ae4cec0afdd'
          '54ff5d2014c2459fb69bddc38cc6162cc79f1a26'
          'aae983b332eb14ae23c6618296ff01a18106ff8f'
          '3fc471b7e45f855f4e3d71d0818ff11648e8e70e'
          '4c6b21a82b00b5089bab47c7bbb143323b36a76b'
          '1f42ba5681006a39bed2f4036802e3d1c731d2ef')

LANG=C
prepare() {
  cd "${srcdir}/${dirname}-${pkgver}"
  sed -i 's/-Wl,-Bstatic//' makefiles/Makefile.linux
  sed -i "s/CFLAGS=-O/CFLAGS=${CFLAGS} -DBUILD_UNTESTED_NEDIT/" makefiles/Makefile.linux
  sed -i 's|"/bin/csh"|"/bin/sh"|' source/preferences.c
}

build() {
  cd ${dirname}-${pkgver}
  make CFLAGS+="${CFLAGS} -DBUILD_UNTESTED_NEDIT" linux docs
}

package() {
  cd ${dirname}-${pkgver}
  install -Dm755 source/nedit "${pkgdir}/usr/bin/nedit"
  install -Dm755 source/nc "${pkgdir}/usr/bin/nedit-client"
  install -Dm644 doc/nedit.man "${pkgdir}/usr/share/man/man1/nedit.1"
  install -Dm644 doc/nc.man "${pkgdir}/usr/share/man/man1/nedit-client.1"
  install -Dm644 doc/nedit.html "${pkgdir}/usr/share/doc/nedit/nedit.html"
  # install .desktop file
  install -Dm755 ../nedit.desktop "${pkgdir}/usr/share/applications/nedit.desktop"
  # install icons
  for r in 256x256 128x128 64x64 48x48 32x32 24x24 16x16
  do
    install -Dm644 ../nedit-${r}.png "${pkgdir}/usr/share/icons/hicolor/${r}/apps/nedit.png"
  done
}

